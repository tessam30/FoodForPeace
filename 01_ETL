/*-------------------------------------------------------------------------------
# Name:		01_ETL
# Purpose:	Create series of folders Food for Peace Time Animated Maps & Analysis
# Author:	Tim Essam, Ph.D.
# Created:	07/23/2014
# Owner:	USAID GeoCenter | OakStream Systems, LLC
# License:	MIT License
# Ado(s):	strReplace
#-------------------------------------------------------------------------------
*/
clear
capture log close

import delimited "$pathin/FFP 60th Country Data 8.11.csv"

***NOTES***
* France 1955 is missing information
* DRC 1964 beht_fswr is beht_fswr $3,363,000,000
* Tanzania 1964 beht_fswr $1,268,000,000
* Togo 1964 beht_fswr 573000000
************

* Fix Kosovo and Serbia country codes
replace iso_num = 381 if countrycode == "KV"
replace iso_num = 688 if countrycode == "RB"

* Fix three countries w/out codes
replace iso_num = 344 if country == "Hong Kong"
replace iso_alpha3 = "HKG" if country== "Hong Kong"
replace iso_alpha2 = "HK" if country == "Hong Kong"

replace iso_num = 446 if country == "Macau"
replace iso_alpha3 = "MAC" if country== "Macau"
replace iso_alpha2 = "MO" if country == "Macau"

replace iso_num = 654 if country == "St. Helena"
replace iso_alpha3 = "SHN" if country== "St. Helena"
replace iso_alpha2 = "SH" if country == "St. Helena"

* Call strReplace_FFP
strReplace_FFP title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole beht_fswr ffp total
format title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole beht_fswr ffp total %16.0gc

* Fix Costa Rica and Greece
*replace title_i = 15000000 if countryname=="Costa Rica" & year == 1992
*replace title_iii = 3562000 if countryname=="Greece" & year ==1964

* Verfiy that all other figures match total
egen double total_check = rowtotal(title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole beht_fswr ffp), mi
format total_check %16.0gc
labgen2 byte total_valid "Total aggregates that match" = (total == total_check) if total!=. & total_check!=.

* Tabulate data and check mis-validated totals
tab total_valid, mi
drop if total_valid==.

* Create variable labels for different titles
la var title_i "Title I: Economic Assistance and Food Security"
la var title_ii_em "Title II: Emergency Assistance Programs"
la var title_ii_dev "Title II: Delopment Food Assistance Programs"
la var title_iii "Title III: Food for Development"
la var title_iv "Title IV: General Authorities and Requirements"
la var b "Section 416(b)"
la var lrp_pilot "Local and Regional Procurement Pilot"
la var mcg_dole "McGovern-Dole International Food for Education and Child Nutrition Program" 
la var beht_fswr "Title V: John Ogonowski and Doug Bereuter Farmer-to-Farmer (F2F) Program"
la var ffp "Food for Peace"


* For now drop missing ISO records
drop if iso_num ==.
bysort iso_num year: gen id = _n

/* Countries with duplicate information
Iraq 368
Seychelles  690
TRINIDAD AND TOBAGO 780
*/

* Write a loop to fix three problem countries. Check totals before/after to ensure valid removal filters
foreach x of numlist 368 690 780 {
	*tab year if iso_num==`x'
	egen tmpCheck`x' = total(total) if iso_num==`x'
	drop if iso_num == `x' & total==0 & id == 2
	egen tmp = total(id) if iso_num == `x', by(year) 
	drop if iso_num == `x' & total ==0 & tmp ==3
	drop tmp
	
	*tab year if iso_num==`x'
	egen tmpCheck2`x' = total(total) if iso_num==`x'
	g byte check`x' = (tmpCheck2`x' == tmpCheck`x')
	drop tmpCheck2`x' tmpCheck`x'
	tab check`x' 
	}
*end

isid iso_num year

* Create ranks for all spending
local titles title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole ffp total total_check 
foreach x of local titles  {
	egen rank_`x' = rank(`x'), by(year) f
}
*end
set more on
local titles title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole ffp total total_check
qui foreach x of local titles {
	*scatter `x' year if rank_`x' == 1, mlabel(countryname) title("`x'") scheme(burd9)
	*more
	}
*
merge m:m year using "$pathin\cpi.dta"
	
*Create adjusted figures to 2013 Dollars
local titles title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole ffp total total_check 
foreach x of local titles {
		g double `x'_2013 = round((`x'/cpi_2013)*100)
		g double ln`x'_2013 = round((`x'/cpi_2013)*100)
		replace ln`x'_2013 = ln(ln`x'_2013)
		copydesc `x' `x'_2013 
		copydesc `x' ln`x'_2013
		*format `x'_2013 %16.0gc
	}
*end
sort iso_num year

* Generate running sum for adjusted and unadjusted totals
bysort iso_num (year): gen culmTotal = sum(total)
bysort iso_num (year): gen culmTotal2013 = sum(total_2013)

*Generate natural logarithm for culmulative totals
g lnculmTotal2013 = ln(culmTotal2013)

egen grandTot = total(total_2013), by(iso_num)
egen annualTot = total(total_2013), by(year)

g lngrantTot = ln(grandTot)
g lnannualTot = ln(annualTot)

* Create country's share for the year
g countryShare = culmTotal2013/grandTot
la var countryShare "Country's annual share of its total value (within)"

* Create country's share for it's own history
g yearlyShare = total_2013/annualTot
la var yearlyShare "Country's annual share of overall value for year (between)"

* Check to make sure everything is summing to 1
assert countryShare==1 | countryShare==. if year==2013
order ln*, last

* Create ranks for all logged spending
local titles title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole ffp total total_check 
foreach x of local titles  {
	egen rank_ln`x' = rank(ln`x'_2013), by(year) f
}
*end

local titles title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole ffp total total_check
qui foreach x of local titles {
	scatter ln`x'_2013 year if rank_ln`x' <= 1, mlabel(countryname) mlabs(tiny) title("`x'") scheme(burd9) by(region)
	more
	}
*

*Create exploratory data analysis graphs
encode country, gen(countryid)
xtset countryid year

set more on
levelsof region, local(levels)
foreach x of local levels {
	xtline countryShare if region=="`x'", tlabel(#3) i(countryname) t(year)
	*more
	*xtline lnculmTotal if region=="`x'", tlabel(#3) i(countryname) t(year)
	more
	}
*end

bob


/* Generate a report of the top 3 values for each year
preserve
	keep if rank_total_check <=5
	keep countryname year rank_total_check title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole beht_fswr ffp total_check total
	*keep countryname year rank_total lntitle_i_2013 lntitle_ii_em_2013 lntitle_ii_dev_2013 lntitle_iii_2013 lntitle_iv_2013 lnb_2013 lnlrp_pilot_2013 lnmcg_dole_2013 lnffp_2013 lntotal_2013
	order countryname year rank_total_check total_check total
	export excel using "$pathxls\FFP_outliers.xls", sheet("Outliers") firstrow(variables) replace
restore

* Export all observations for which reported total does not match calculated total
preserve
	keep if total_valid==0
	keep countryname year title_i title_ii_em title_ii_dev title_iii title_iv b lrp_pilot mcg_dole beht_fswr ffp total_check total
	order countryname year total_check total
	export excel using "$pathxls/FFP_outliers.xls", sheet("Totals_Different") firstrow(variables) sheetmodify
restore
*/
	
* Export a copy of the data to R for playing in GGplot anad GGVIS
save "$pathout/FTF_processed.dta", replace 
saveold "$pathout/FTF_processed.dta", replace
